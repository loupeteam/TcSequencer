<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Sequencer" Id="{6cce11bf-bf75-4b96-8542-c7369e25211e}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Sequencer IMPLEMENTS ISequence, IPlugin, IPLCopenCall
VAR_INPUT	
  startStep : ENUM;
  Command : SequenceCommand;
END_VAR
VAR
  _sequence : SequenceType;
  _activeTasks : PLCopenCallGroup;
  _stepHandler : IStepManager;
  _baseManager : StepManagerBasic;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.]]></ST>
    </Implementation>
    <Folder Name="API" Id="{939f7e8c-9d25-4182-9029-745e8da56e72}">
      <Folder Name="Decisions" Id="{877b3894-7f7e-462b-b8bc-c682c2070163}" />
      <Folder Name="StateControl" Id="{6abf5737-70bc-45fb-a397-53c0f97ae526}" />
      <Folder Name="StateTransition" Id="{4654304c-709e-4a34-bdc7-0fda0c88f45a}" />
    </Folder>
    <Folder Name="Internal" Id="{e6aa5ed2-3892-4dae-8fd4-cfebc470258a}">
      <Folder Name="PLCopenCaller" Id="{692cb7d4-90ad-40a3-84ed-e86faabe0e62}" />
    </Folder>
    <Method Name="CheckStatus" Id="{6b7c3f12-8739-4096-9480-9fa604b0d507}" FolderPath="Internal\PLCopenCaller\">
      <Declaration><![CDATA[
METHOD PRIVATE CheckStatus : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

IF NOT _sequence.statusChecked THEN
  _sequence.statusChecked := TRUE;
  // Cache the abort so there's a consistent status for the whole state
  _sequence.isError := _activeTasks.Error;
  _sequence.isAbort := _activeTasks.CommandAborted;
  _sequence.isDone := _activeTasks.Done;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="CurrentStep" Id="{b7b67642-2d40-4a87-a0bc-1df8b62b202f}" FolderPath="API\">
      <Declaration><![CDATA[
{attribute 'monitoring' := 'call'}
PROPERTY CurrentStep : ENUM
]]></Declaration>
      <Get Name="Get" Id="{5106b701-efa0-4091-93e9-d0347cc8550a}">
        <Declaration><![CDATA[
]]></Declaration>
        <Implementation>
          <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

CurrentStep := _stepHandler.CurrentStepImpl;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="DoStep" Id="{b100a4e3-ede6-467d-8a89-6654bcb8c169}" FolderPath="API\">
      <Declaration><![CDATA[
METHOD DoStep : ENUM
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.


IF Command.Start.Consume THEN
  _stepHandler.RequestStepImpl(startStep);
END_IF

_sequence.currentStep := _stepHandler.CurrentStepImpl;

//If the current step has changed,
//	Set the flag for onEntry()
IF _stepHandler.RequestNumberImpl <> _sequence.lastRequestNumber THEN

  _sequence.lastRequestNumber := _stepHandler.RequestNumberImpl;
  _sequence.onEntry := TRUE;
  _sequence.onExit := FALSE;
ELSE
  _sequence.onEntry := FALSE;
END_IF

//The start step is used to detect if the 
_sequence.startStep := _sequence.currentStep;

//Mark that we haven't checked the status yet, 
//	This is to ensure we don't have inconsitent onCalls
_sequence.statusChecked := FALSE;
_sequence.errorHandled := FALSE;

IF _sequence.onExit THEN
  DoStep := SEQUENCE_STEP.NO_STEP;
ELSE
  //Return the state
  DoStep := _sequence.currentStep;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{f87966c2-0adb-4c01-87f6-71b336bb9cc0}" FolderPath="Internal\">
      <Declaration><![CDATA[
METHOD FB_init : BOOL
VAR_INPUT
  bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
  bInCopyCode : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
  stepHandler : IStepManager;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

IF stepHandler = 0 THEN
  _stepHandler := _baseManager;
ELSE
  _stepHandler := stepHandler;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="IsAbort" Id="{dbaf10d5-3866-4d96-b10f-98bd3ccf9a22}" FolderPath="Internal\PLCopenCaller\">
      <Declaration><![CDATA[
PROPERTY IsAbort : BOOL
]]></Declaration>
      <Get Name="Get" Id="{06516dd3-b590-4e87-bf5f-fded754e45e8}">
        <Declaration><![CDATA[
]]></Declaration>
        <Implementation>
          <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

CheckStatus();
IsAbort := _sequence.isAbort;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="IsDone" Id="{0abb87b3-c33c-4d5b-bab6-668ee8ff9e26}" FolderPath="Internal\PLCopenCaller\">
      <Declaration><![CDATA[
PROPERTY IsDone : BOOL
]]></Declaration>
      <Get Name="Get" Id="{0c5d4645-ee28-4cc6-87d7-b772cfb09f31}">
        <Declaration><![CDATA[
]]></Declaration>
        <Implementation>
          <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

CheckStatus();
IsDone := _sequence.isDone;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="IsError" Id="{45d11189-3838-46ca-97d2-70f9988c183a}" FolderPath="Internal\PLCopenCaller\">
      <Declaration><![CDATA[
PROPERTY IsError : BOOL
]]></Declaration>
      <Get Name="Get" Id="{8849948e-45c2-441d-abda-6c306a83e77e}">
        <Declaration><![CDATA[
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

CheckStatus();
IsError := _sequence.isError;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnAbort" Id="{124a7554-7af0-48f3-9c3b-c6fcfb012c1a}" FolderPath="API\Decisions\">
      <Declaration><![CDATA[
METHOD OnAbort : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

// Check all of the status to see if we are aborting

IF IsAbort THEN

  IF nextStep >= 0 THEN
    LogMessage(1, 'Step Abort');
    _sequence.errorHandled := TRUE;
    setNextStep(nextStep);
  END_IF

  OnAbort := TRUE;
ELSE
  OnAbort := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnEntry" Id="{d5293258-9127-40f2-8767-4af37daea8dd}" FolderPath="API\StateTransition\">
      <Declaration><![CDATA[
METHOD OnEntry : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

//If the current step has since the doStep
OnEntry := _sequence.onEntry;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{2b381ad0-9511-4a2a-a59b-89ff6fa0eadc}" FolderPath="API\Decisions\">
      <Declaration><![CDATA[
METHOD OnError : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

IF IsError THEN
  IF nextStep >= 0 THEN
    LogMessage(1, 'Step Abort');
    _sequence.errorHandled := TRUE;
    setNextStep(nextStep);
  END_IF

  OnError := TRUE;
ELSE
  OnError := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExit" Id="{f59d2d40-7d9d-447e-9f98-0b5c912393f5}" FolderPath="API\StateTransition\">
      <Declaration><![CDATA[METHOD OnExit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

//If the current step has since the doStep
OnExit := _sequence.onExit;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnFailure" Id="{8864cad6-d51a-434b-b76a-3fb1f56e20e0}" FolderPath="API\Decisions\">
      <Declaration><![CDATA[
METHOD OnFailure : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

IF IsAbort OR IsError THEN
  //On failure will not set the step if a more specific handler already set the step
  IF _sequence.errorHandled = FALSE AND nextStep >= 0 THEN
    LogMessage(1, 'Step Failure');
    setNextStep(nextStep);
  END_IF

  OnFailure := TRUE;
ELSE
  OnFailure := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnSuccess" Id="{2bf2939a-63ed-4f80-94f9-d2fc5becbe2f}" FolderPath="API\Decisions\">
      <Declaration><![CDATA[
METHOD OnSuccess : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

IF IsDone THEN
  IF nextStep >= 0 THEN
    LogMessage(1, 'Step Success');
    setNextStep(nextStep);
  END_IF

  OnSuccess := TRUE;
ELSE
  OnSuccess := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestStep" Id="{b48e0562-c987-4989-8061-de7cf0c6fe2a}" FolderPath="API\StateControl\">
      <Declaration><![CDATA[
METHOD RequestStep : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

_stepHandler.RequestStepImpl(nextStep);
_sequence.onExit := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="RequestStepClear" Id="{a486ef5e-3d7c-419f-8255-b0a39dabc8fc}" FolderPath="API\StateControl\">
      <Declaration><![CDATA[
METHOD RequestStepClear : BOOL
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

RequestStep(nextStep);
_activeTasks.ResetStatus();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Run" Id="{77a6af61-d3f7-4c0a-b1d9-a63defd9e3b9}" FolderPath="Internal\">
      <Declaration><![CDATA[
METHOD Run : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

THIS^();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SequenceAbort" Id="{cc629ce3-ace3-4ca4-872b-7b49e09a5e57}" FolderPath="API\StateControl\">
      <Declaration><![CDATA[
METHOD SequenceAbort : BOOL
VAR_INPUT
  ErrorCode : DINT;
  ErrorMessage : STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

_stepHandler.SequenceErrorImpl(DINT_TO_INT(ErrorCode), ErrorMessage);
_activeTasks.ResetStatus();
_sequence.onExit := TRUE;
Command.Start.Aborted();
SequenceAbort := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SequenceComplete" Id="{f1ae842d-4867-4029-a65e-180592997a50}" FolderPath="API\StateControl\">
      <Declaration><![CDATA[
METHOD SequenceComplete : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

_stepHandler.SequenceCompleteImpl();
_activeTasks.ResetStatus();
_sequence.onExit := TRUE;
Command.Start.Finished();
SequenceComplete := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SequenceError" Id="{06555983-6dc8-4375-b176-7e8590c26dcd}" FolderPath="API\StateControl\">
      <Declaration><![CDATA[
METHOD SequenceError : BOOL
VAR_INPUT
  ErrorCode : INT;
  ErrorMessage : STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

_stepHandler.SequenceErrorImpl(ErrorCode, ErrorMessage);
_activeTasks.ResetStatus();
_sequence.onExit := TRUE;
Command.Start.Errored(ErrorCode);
SequenceError := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetNextStep" Id="{c0c17481-86c4-4051-a78b-3b1ee372be58}" FolderPath="Internal\">
      <Declaration><![CDATA[
METHOD PROTECTED SetNextStep
VAR_INPUT
  nextStep : ENUM;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.

_stepHandler.RequestStepImpl(nextStep);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{52f1f4d2-13d6-46fb-aa1f-c1affcffbade}" FolderPath="Internal\PLCopenCaller\">
      <Declaration><![CDATA[
METHOD Start : PLCopenStatusBase
VAR_INPUT
  Command : IPLCopenCommand;
END_VAR
VAR
	_status : PLCopenStatusBase;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.
_status := _activeTasks.Start(Command);
IF  _status <> PLCopenStatusBase.BUSY THEN
  // TODO: All out of commands.. Throw an error
  sequenceError(10000, 'Too Many Commands in 1 step');
END_IF
Start := _status;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="StartSequence" Id="{3c531c5d-3c1d-4272-888a-ef0a2888f2c8}" FolderPath="API\">
      <Declaration><![CDATA[
METHOD StartSequence : BOOL
VAR_INPUT
	Status : IPLCopenCaller;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Copyright (c) 2024 Loupe (https://loupe.team), provided under the MIT License.
IF Status <> 0 THEN
	Status.Start(Command.Start);
ELSE
	Command.Start.Execute := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>